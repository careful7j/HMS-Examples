def getProperties(propFile) {
    def propertiesFile = rootProject.file(propFile)
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return properties
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.huawei.agconnect'
    id 'kotlin-parcelize'
}

android {
    namespace 'net.c7j.wna.huawei'
    compileSdk rootProject.compilesdk

    defaultConfig {
        //Note: this id belongs to production application configured in a special way
        applicationId "net.c7j.wna.huawei"
        minSdk rootProject.minsdk
        targetSdk rootProject.targetsdk
        versionCode 1
        versionName "1.0"
        resConfigs "en"  // drop unused resources overhead to speed up examples build
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions { jvmTarget = '1.8' }

    // It is important to sign your .apk, so HMS can auth success
    Properties signProperties = getProperties('keystore.properties')
    signingConfigs {
        release {
            storeFile rootProject.file(signProperties['storeFile'])
            storePassword signProperties['storePassword']
            keyAlias signProperties['keyAlias']
            keyPassword signProperties['keyPassword']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            debuggable true // only for examples, don't do it in your production build
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation "androidx.core:core-ktx:$rootProject.ver.corektx"
    implementation "androidx.appcompat:appcompat:$rootProject.ver.appcompat"
    implementation "com.google.android.material:material:$rootProject.ver.material"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ver.constraintlayout"

    implementation project(path: ':app:appResourceLibrary')
    implementation project(path: ':app:analytics')
    implementation project(path: ':app:ads')
    implementation project(path: ':app:location')
    implementation project(path: ':app:maps')
    implementation project(path: ':app:push')
    implementation project(path: ':app:in-app-purchases')
    implementation project(path: ':app:fidokit-fingerprint')
}