pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://developer.huawei.com/repo/' }
    }
    plugins {
        id 'com.android.application' version '8.2.0'
        id 'org.jetbrains.kotlin.android' version '1.9.21'
    }
    resolutionStrategy {
        eachPlugin {
            if (it.requested.id.getNamespace() == 'com.huawei.agconnect') {
                println ">> ${it.requested.id.id}"
                if (it.requested.id.id == 'com.huawei.agconnect.agcp') {
                    it.useModule('com.huawei.agconnect:agcp:1.9.1.301')
                }
                println ">> ${it.target}"
            } else {
                println ">  ${it.target}"
            }
        }
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
    }
}
rootProject.name = "HMS-Examples"
include ':app:appResourceLibrary'
include ':app'
include ':app:analytics'
include ':app:ads'
include ':app:location'
include ':app:maps'
include ':app:push'
include ':app:in-app-purchases'
include ':app:fidokit-fingerprint'
include ':app:safety-detect'
include ':app:account-sms'
include ':app:scan'
include ':app:ml-kit'
